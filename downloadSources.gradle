//下载依赖源码脚本,fork from- AS [DownloadSource] task
subprojects {
  afterEvaluate { download(it) }
}

/****************下载依赖源码-start*******************/
static void download(Project project) {
    def overwrite = project.tasks.findByName('DownloadAllSources') != null
    project.tasks.create(name: 'DownloadAllSources', overwrite: overwrite) {
        doLast {
            def repoList = project.repositories.toList()
            if (repoList == null || repoList.isEmpty()) {
                repoList = project.rootProject.repositories.toList()
            }
            project.configurations.all { Configuration conf ->
                if (conf.name.contains("api")
                        || conf.name.contains("compile")
                        || conf.name.contains("compileOnly")
                        || conf.name.contains("implementation")) {
                    conf.getDependencies().toList().forEach({ dep ->
                        if (checkValid(dep)) {
                            def depSources = "${dep.group}:${dep.name}:${dep.version}:sources"
                            downloadSourcesTemp(project, depSources, repoList)
                        }
                    })
                }
            }

        }
    }
}

static boolean checkValid(Dependency it) {
    return !(it == null || it.group == null || it.name == null || it.version == null || it.version == "unspecified")
}

static void downloadSourcesTemp(Project project, String depSources, List<ArtifactRepository> repoList) {
    if (repoList == null || repoList.isEmpty()) {
        project.logger.log(LogLevel.ERROR, "repoList is empty, finish-----($depSources)")
        return
    }
    def configuration = null
    def repoName = null
    def repository = repoList.find {
        repoName = "$it.name"
        //project.logger.lifecycle("Attempt to download sources from $repoName ----- $depSources")
        project.repositories.clear()
        project.repositories.add(it)
        configuration = project.configurations.create('downloadSourcesFrom_' + UUID.randomUUID())
        configuration.transitive = false
        project.dependencies.add(configuration.name, depSources)
        def files = null
        try {
            files = configuration.resolvedConfiguration.lenientConfiguration.getFiles()
        } catch (Throwable ignore) {
        }
        return files && !files.isEmpty()
    }
    if (repository != null) {
        configuration = project.configurations.create('downloadSources_' + UUID.randomUUID())
        configuration.transitive = false
        project.dependencies.add(configuration.name, depSources)
        configuration.resolve()
    } else {
        if (depSources.endsWith("sources")) {
            def depSource = depSources.replace("sources", "source")
            downloadSourcesTemp(project, depSource, repoList)
            return
        }
    }
    try {
        def sourcesPath = configuration?.singleFile?.path
        if (sourcesPath) {
            project.logger.lifecycle('Sources were downloaded to ' + sourcesPath)
        } else {
            if (depSources.endsWith("sources")) {
                def depSource = depSources.replace("sources", "source")
                downloadSourcesTemp(depSource)
            } else {
                throw new RuntimeException('Sources download failed')
            }
        }
    } catch (Throwable ignore) {
        project.logger.log(LogLevel.ERROR, "downlod failed($repoName)-----$depSources")
    }
}
/****************下载依赖源码-end*******************/
